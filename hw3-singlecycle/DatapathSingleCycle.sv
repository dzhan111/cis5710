`timescale 1ns / 1ns

// registers are 32 bits in RV32
`define REG_SIZE 31:0

// insns are 32 bits in RV32IM
`define INSN_SIZE 31:0

// RV opcodes are 7 bits
`define OPCODE_SIZE 6:0

`include "../hw2a-divider/DividerUnsigned.sv"
`include "../hw2b-cla/CarryLookaheadAdder.sv"
`include "cycle_status.sv"

module RegFile (
    input logic [4:0] rd,
    input logic [`REG_SIZE] rd_data,
    input logic [4:0] rs1,
    output logic [`REG_SIZE] rs1_data,
    input logic [4:0] rs2,
    output logic [`REG_SIZE] rs2_data,

    input logic clk,
    input logic we,
    input logic rst
);
  localparam int NumRegs = 32;
  logic [`REG_SIZE] regs[NumRegs];

  assign rs1_data = (rs1==0)? '0 : regs[rs1];
  assign rs2_data = (rs2==0)? '0 : regs[rs2];

  always_ff @(posedge clk) begin
    if (rst) begin
      for (int i = 0; i < NumRegs; i++) begin
        regs[i] <= '0;
      end
    end else if (we && rd != 5'd0) begin
      regs[rd] <= rd_data;
    end
  end

endmodule

module DatapathSingleCycle (
    input wire                clk,
    input wire                rst,
    output logic              halt,
    output logic [`REG_SIZE]  pc_to_imem,
    input wire [`INSN_SIZE]   insn_from_imem,
    // addr_to_dmem is used for both loads and stores
    output logic [`REG_SIZE]  addr_to_dmem,
    input logic [`REG_SIZE]   load_data_from_dmem,
    output logic [`REG_SIZE]  store_data_to_dmem,
    output logic [3:0]        store_we_to_dmem,

    // the PC of the insn executing in the current cycle
    output logic [`REG_SIZE]  trace_completed_pc,
    // the machine code of the insn executing in the current cycle
    output logic [`INSN_SIZE] trace_completed_insn,
    // the cycle status of the current cycle: should always be CYCLE_NO_STALL
    output cycle_status_e     trace_completed_cycle_status
);

  // components of the instruction
  wire [6:0] insn_funct7;
  wire [4:0] insn_rs2;
  wire [4:0] insn_rs1;
  wire [2:0] insn_funct3;
  wire [4:0] insn_rd;
  wire [`OPCODE_SIZE] insn_opcode;

  // split R-type instruction - see section 2.2 of RiscV spec
  assign {insn_funct7, insn_rs2, insn_rs1, insn_funct3, insn_rd, insn_opcode} = insn_from_imem;

  // setup for I, S, B & J type instructions
  // I - short immediates and loads
  wire [11:0] imm_i;
  assign imm_i = insn_from_imem[31:20];
  wire [ 4:0] imm_shamt = insn_from_imem[24:20];

  // S - stores
  wire [11:0] imm_s;
  assign imm_s = {insn_from_imem[31:25], insn_from_imem[11:7]};

  // B - conditionals
  wire [12:0] imm_b;
  assign imm_b = {insn_from_imem[31],      // imm[12]
                  insn_from_imem[7],       // imm[11]
                  insn_from_imem[30:25],   // imm[10:5]
                  insn_from_imem[11:8],    // imm[4:1]
                  1'b0};                   // imm[0]

  // J - unconditional jumps
  wire [20:0] imm_j;
  assign imm_j = {insn_from_imem[31],      // imm[20]
                  insn_from_imem[19:12],   // imm[19:12]
                  insn_from_imem[20],      // imm[11]
                  insn_from_imem[30:21],   // imm[10:1]
                  1'b0};                   // imm[0]


  wire [`REG_SIZE] imm_i_sext = {{20{imm_i[11]}}, imm_i[11:0]};
  wire [`REG_SIZE] imm_s_sext = {{20{imm_s[11]}}, imm_s[11:0]};
  wire [`REG_SIZE] imm_b_sext = {{19{imm_b[12]}}, imm_b[12:0]};
  wire [`REG_SIZE] imm_j_sext = {{11{imm_j[20]}}, imm_j[20:0]};
  wire [`REG_SIZE] imm_u = {insn_from_imem[31:12], 12'b0};

  // opcodes - see section 19 of RiscV spec
  localparam bit [`OPCODE_SIZE] OpLoad = 7'b00_000_11;
  localparam bit [`OPCODE_SIZE] OpStore = 7'b01_000_11;
  localparam bit [`OPCODE_SIZE] OpBranch = 7'b11_000_11;
  localparam bit [`OPCODE_SIZE] OpJalr = 7'b11_001_11;
  localparam bit [`OPCODE_SIZE] OpMiscMem = 7'b00_011_11;
  localparam bit [`OPCODE_SIZE] OpJal = 7'b11_011_11;

  localparam bit [`OPCODE_SIZE] OpRegImm = 7'b00_100_11;
  localparam bit [`OPCODE_SIZE] OpRegReg = 7'b01_100_11;
  localparam bit [`OPCODE_SIZE] OpEnviron = 7'b11_100_11;

  localparam bit [`OPCODE_SIZE] OpAuipc = 7'b00_101_11;
  localparam bit [`OPCODE_SIZE] OpLui = 7'b01_101_11;

  wire insn_lui   = insn_opcode == OpLui;
  wire insn_auipc = insn_opcode == OpAuipc;
  wire insn_jal   = insn_opcode == OpJal;
  wire insn_jalr  = insn_opcode == OpJalr;

  wire insn_beq  = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b000;
  wire insn_bne  = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b001;
  wire insn_blt  = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b100;
  wire insn_bge  = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b101;
  wire insn_bltu = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b110;
  wire insn_bgeu = insn_opcode == OpBranch && insn_from_imem[14:12] == 3'b111;

  wire insn_lb  = insn_opcode == OpLoad && insn_from_imem[14:12] == 3'b000;
  wire insn_lh  = insn_opcode == OpLoad && insn_from_imem[14:12] == 3'b001;
  wire insn_lw  = insn_opcode == OpLoad && insn_from_imem[14:12] == 3'b010;
  wire insn_lbu = insn_opcode == OpLoad && insn_from_imem[14:12] == 3'b100;
  wire insn_lhu = insn_opcode == OpLoad && insn_from_imem[14:12] == 3'b101;

  wire insn_sb = insn_opcode == OpStore && insn_from_imem[14:12] == 3'b000;
  wire insn_sh = insn_opcode == OpStore && insn_from_imem[14:12] == 3'b001;
  wire insn_sw = insn_opcode == OpStore && insn_from_imem[14:12] == 3'b010;

  wire insn_addi  = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b000;
  wire insn_slti  = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b010;
  wire insn_sltiu = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b011;
  wire insn_xori  = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b100;
  wire insn_ori   = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b110;
  wire insn_andi  = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b111;

  wire insn_slli = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b001 && insn_from_imem[31:25] == 7'd0;
  wire insn_srli = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b101 && insn_from_imem[31:25] == 7'd0;
  wire insn_srai = insn_opcode == OpRegImm && insn_from_imem[14:12] == 3'b101 && insn_from_imem[31:25] == 7'b0100000;

  wire insn_add  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b000 && insn_from_imem[31:25] == 7'd0;
  wire insn_sub  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b000 && insn_from_imem[31:25] == 7'b0100000;
  wire insn_sll  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b001 && insn_from_imem[31:25] == 7'd0;
  wire insn_slt  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b010 && insn_from_imem[31:25] == 7'd0;
  wire insn_sltu = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b011 && insn_from_imem[31:25] == 7'd0;
  wire insn_xor  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b100 && insn_from_imem[31:25] == 7'd0;
  wire insn_srl  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b101 && insn_from_imem[31:25] == 7'd0;
  wire insn_sra  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b101 && insn_from_imem[31:25] == 7'b0100000;
  wire insn_or   = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b110 && insn_from_imem[31:25] == 7'd0;
  wire insn_and  = insn_opcode == OpRegReg && insn_from_imem[14:12] == 3'b111 && insn_from_imem[31:25] == 7'd0;

  wire insn_mul    = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b000;
  wire insn_mulh   = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b001;
  wire insn_mulhsu = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b010;
  wire insn_mulhu  = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b011;
  wire insn_div    = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b100;
  wire insn_divu   = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b101;
  wire insn_rem    = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b110;
  wire insn_remu   = insn_opcode == OpRegReg && insn_from_imem[31:25] == 7'd1 && insn_from_imem[14:12] == 3'b111;

  wire insn_ecall = insn_opcode == OpEnviron && insn_from_imem[31:7] == 25'd0;
  wire insn_fence = insn_opcode == OpMiscMem;

  // this code is only for simulation, not synthesis
  `ifndef SYNTHESIS
  `include "RvDisassembler.sv"
  string disasm_string;
  always_comb begin
    disasm_string = rv_disasm(insn_from_imem);
  end
  // HACK: get disasm_string to appear in GtkWave, which can apparently show only wire/logic...
  wire [(8*32)-1:0] disasm_wire;
  genvar i;
  for (i = 0; i < 32; i = i + 1) begin : gen_disasm
    assign disasm_wire[(((i+1))*8)-1:((i)*8)] = disasm_string[31-i];
  end
  `endif

  // program counter
  logic [`REG_SIZE] pcNext, pcCurrent;
  always @(posedge clk) begin
    if (rst) begin
      pcCurrent <= 32'd0;
    end else begin
      pcCurrent <= pcNext;
    end
  end
  assign pc_to_imem = pcCurrent;
  assign trace_completed_pc = pcCurrent;
  assign trace_completed_insn = insn_from_imem;
  assign trace_completed_cycle_status = CYCLE_NO_STALL;

  // cycle/insn_from_imem counters
  logic [`REG_SIZE] cycles_current, num_insns_current;
  always @(posedge clk) begin
    if (rst) begin
      cycles_current <= 0;
      num_insns_current <= 0;
    end else begin
      cycles_current <= cycles_current + 1;
      if (!rst) begin
        num_insns_current <= num_insns_current + 1;
      end
    end
  end

  logic        rf_we;
  logic [4:0]  rf_rd, rf_rs1, rf_rs2;
  logic [`REG_SIZE] rf_wdata;

  wire [`REG_SIZE] rs1_data;
  wire [`REG_SIZE] rs2_data;

  RegFile rf (
    .clk(clk),
    .rst(rst),
    .we(rf_we),
    .rd(rf_rd),
    .rd_data(rf_wdata),
    .rs1(rf_rs1),
    .rs2(rf_rs2),
    .rs1_data(rs1_data),
    .rs2_data(rs2_data));
  
  logic [`REG_SIZE] cla_a, cla_b;
  logic cla_cin;
  wire [`REG_SIZE] cla_sum;

  CarryLookaheadAdder cla (
    .a(cla_a),
    .b(cla_b),
    .cin(cla_cin),
    .sum(cla_sum));

  wire rs1_neg = rs1_data[31];
  wire rs2_neg = rs2_data[31];
  wire div_result_neg = rs1_neg ^ rs2_neg;

  wire [`REG_SIZE] rs1_abs = rs1_neg ? (~rs1_data + 32'd1) : rs1_data;
  wire [`REG_SIZE] rs2_abs = rs2_neg ? (~rs2_data + 32'd1) : rs2_data;

  wire [63:0] mul_unsigned_full = {32'd0, rs1_data} * {32'd0, rs2_data};
  wire [63:0] mul_signed_abs_full = {32'd0, rs1_abs} * {32'd0, rs2_abs};
  wire [63:0] mulhsu_abs_full = {32'd0, rs1_abs} * {32'd0, rs2_data};

  wire [63:0] mul_signed_full = div_result_neg ? (~mul_signed_abs_full + 64'd1) : mul_signed_abs_full;
  wire [63:0] mulhsu_full = rs1_neg ? (~mulhsu_abs_full + 64'd1) : mulhsu_abs_full;

  wire do_signed_divrem = insn_div || insn_rem;
  wire [`REG_SIZE] div_dividend = do_signed_divrem ? rs1_abs : rs1_data;
  wire [`REG_SIZE] div_divisor = do_signed_divrem ? rs2_abs : rs2_data;
  wire [`REG_SIZE] div_remainder_raw;
  wire [`REG_SIZE] div_quotient_raw;

  DividerUnsigned divider (
    .i_dividend(div_dividend),
    .i_divisor(div_divisor),
    .o_remainder(div_remainder_raw),
    .o_quotient(div_quotient_raw));

  wire signed_div_overflow = (rs1_data == 32'h8000_0000) && (rs2_data == 32'hFFFF_FFFF);

  wire [`REG_SIZE] load_addr = rs1_data + imm_i_sext;
  wire [`REG_SIZE] store_addr = rs1_data + imm_s_sext;
  wire [1:0] load_byte_offset = load_addr[1:0];
  wire [1:0] store_byte_offset = store_addr[1:0];
  wire [`REG_SIZE] load_word_shifted = load_data_from_dmem >> ({27'd0, load_byte_offset} << 3);

  logic illegal_insn;
  logic taken;

  always_comb begin
    illegal_insn = 1'b0;

    halt = 1'b0;

    rf_we    = 1'b0;
    rf_rd    = 5'd0;
    rf_rs1   = insn_rs1;
    rf_rs2   = insn_rs2;
    rf_wdata = '0;

    cla_a   = '0;
    cla_b   = '0;
    cla_cin = 1'b0;

    addr_to_dmem       = '0;
    store_data_to_dmem = '0;
    store_we_to_dmem   = 4'b0000;

    taken = 1'b0;

    if (rst) begin
      pcNext = pcCurrent;
    end else begin
      pcNext = pcCurrent + 32'd4;

      if (insn_ecall) begin
        halt = 1'b1;
      end else if (insn_fence) begin
      end
      else begin
        case (insn_opcode)
          OpLui: begin
            rf_we = 1'b1;
            rf_rd = insn_rd;
            rf_wdata = imm_u;
          end
          OpAuipc: begin
            rf_we = 1'b1;
            rf_rd = insn_rd;
            rf_wdata = pcCurrent + imm_u;
          end
          OpJal: begin
            rf_we = 1'b1;
            rf_rd = insn_rd;
            rf_wdata = pcCurrent + 32'd4;
            pcNext = pcCurrent + imm_j_sext;
          end
          OpJalr: begin
            rf_we = 1'b1;
            rf_rd = insn_rd;
            rf_wdata = pcCurrent + 32'd4;
            pcNext = (rs1_data + imm_i_sext) & 32'hFFFF_FFFE;
          end
          OpLoad: begin
            rf_we = 1'b1;
            rf_rd = insn_rd;
            addr_to_dmem = {load_addr[31:2], 2'b00};

            if (insn_lb) begin
              rf_wdata = {{24{load_word_shifted[7]}}, load_word_shifted[7:0]};
            end
            else if (insn_lbu) begin
              rf_wdata = {24'd0, load_word_shifted[7:0]};
            end
            else if (insn_lh) begin
              rf_wdata = {{16{load_word_shifted[15]}}, load_word_shifted[15:0]};
            end
            else if (insn_lhu) begin
              rf_wdata = {16'd0, load_word_shifted[15:0]};
            end
            else if (insn_lw) begin
              rf_wdata = load_data_from_dmem;
            end
            else begin
              illegal_insn = 1'b1;
            end
          end
          OpStore: begin
            addr_to_dmem = {store_addr[31:2], 2'b00};

            if (insn_sb) begin
              store_data_to_dmem = {4{rs2_data[7:0]}};
              store_we_to_dmem = 4'b0001 << store_byte_offset;
            end
            else if (insn_sh) begin
              store_data_to_dmem = {2{rs2_data[15:0]}};
              store_we_to_dmem = store_byte_offset[1] ? 4'b1100 : 4'b0011;
            end
            else if (insn_sw) begin
              store_data_to_dmem = rs2_data;
              store_we_to_dmem = 4'b1111;
            end
            else begin
              illegal_insn = 1'b1;
            end
          end
          OpRegImm: begin
            rf_rd = insn_rd;

            if (insn_addi) begin
              rf_we   = 1'b1;
              cla_a   = rs1_data;
              cla_b   = imm_i_sext;
              cla_cin = 1'b0;
              rf_wdata = cla_sum;
            end
            else if (insn_slti) begin
              rf_we    = 1'b1;
              rf_wdata = ($signed(rs1_data) < $signed(imm_i_sext)) ? 32'd1 : 32'd0;
            end
            else if (insn_sltiu) begin
              rf_we    = 1'b1;
              rf_wdata = (rs1_data < imm_i_sext) ? 32'd1 : 32'd0;
            end
            else if (insn_xori) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data ^ imm_i_sext;
            end
            else if (insn_ori) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data | imm_i_sext;
            end
            else if (insn_andi) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data & imm_i_sext;
            end
            else if (insn_slli) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data << imm_shamt;
            end
            else if (insn_srli) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data >> imm_shamt;
            end
            else if (insn_srai) begin
              rf_we    = 1'b1;
              rf_wdata = $signed(rs1_data) >>> imm_shamt;
            end
            else begin
              illegal_insn = 1'b1;
            end
          end
          OpRegReg: begin
            rf_rd = insn_rd;

            if (insn_add) begin
              rf_we   = 1'b1;
              cla_a   = rs1_data;
              cla_b   = rs2_data;
              cla_cin = 1'b0;
              rf_wdata = cla_sum;
            end
            else if (insn_sub) begin
              rf_we   = 1'b1;
              cla_a   = rs1_data;
              cla_b   = ~rs2_data;
              cla_cin = 1'b1;
              rf_wdata = cla_sum;
            end
            else if (insn_sll) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data << rs2_data[4:0];
            end
            else if (insn_slt) begin
              rf_we    = 1'b1;
              rf_wdata = ($signed(rs1_data) < $signed(rs2_data)) ? 32'd1 : 32'd0;
            end
            else if (insn_sltu) begin
              rf_we    = 1'b1;
              rf_wdata = (rs1_data < rs2_data) ? 32'd1 : 32'd0;
            end
            else if (insn_xor) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data ^ rs2_data;
            end
            else if (insn_srl) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data >> rs2_data[4:0];
            end
            else if (insn_sra) begin
              rf_we    = 1'b1;
              rf_wdata = $signed(rs1_data) >>> rs2_data[4:0];
            end
            else if (insn_or) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data | rs2_data;
            end
            else if (insn_and) begin
              rf_we    = 1'b1;
              rf_wdata = rs1_data & rs2_data;
            end
            else if (insn_mul) begin
              rf_we = 1'b1;
              rf_wdata = mul_unsigned_full[31:0];
            end
            else if (insn_mulh) begin
              rf_we = 1'b1;
              rf_wdata = mul_signed_full[63:32];
            end
            else if (insn_mulhsu) begin
              rf_we = 1'b1;
              rf_wdata = mulhsu_full[63:32];
            end
            else if (insn_mulhu) begin
              rf_we = 1'b1;
              rf_wdata = mul_unsigned_full[63:32];
            end
            else if (insn_div) begin
              rf_we = 1'b1;
              if (rs2_data == 32'd0) begin
                rf_wdata = 32'hFFFF_FFFF;
              end
              else if (signed_div_overflow) begin
                rf_wdata = 32'h8000_0000;
              end
              else begin
                rf_wdata = div_result_neg ? (~div_quotient_raw + 32'd1) : div_quotient_raw;
              end
            end
            else if (insn_divu) begin
              rf_we = 1'b1;
              if (rs2_data == 32'd0) begin
                rf_wdata = 32'hFFFF_FFFF;
              end
              else begin
                rf_wdata = div_quotient_raw;
              end
            end
            else if (insn_rem) begin
              rf_we = 1'b1;
              if (rs2_data == 32'd0) begin
                rf_wdata = rs1_data;
              end
              else if (signed_div_overflow) begin
                rf_wdata = 32'd0;
              end
              else begin
                rf_wdata = rs1_neg ? (~div_remainder_raw + 32'd1) : div_remainder_raw;
              end
            end
            else if (insn_remu) begin
              rf_we = 1'b1;
              if (rs2_data == 32'd0) begin
                rf_wdata = rs1_data;
              end
              else begin
                rf_wdata = div_remainder_raw;
              end
            end
            else begin
              illegal_insn = 1'b1;
            end
          end
          OpBranch: begin
            if (insn_beq)      taken = (rs1_data == rs2_data);
            else if (insn_bne) taken = (rs1_data != rs2_data);
            else if (insn_blt) taken = ($signed(rs1_data) < $signed(rs2_data));
            else if (insn_bge) taken = ($signed(rs1_data) >= $signed(rs2_data));
            else if (insn_bltu) taken = (rs1_data < rs2_data);
            else if (insn_bgeu) taken = (rs1_data >= rs2_data);
            else illegal_insn = 1'b1;

            if (taken) pcNext = pcCurrent + imm_b_sext;
          end
          default: begin
            illegal_insn = 1'b1;
          end
        endcase
      end
    end
  end

endmodule

/* A memory module that supports 1-cycle reads and writes, with one read-only port
 * and one read+write port.
 */
module MemorySingleCycle #(
    parameter int NUM_WORDS = 512
) (
    // rst for both imem and dmem
    input wire rst,

    // clock for both imem and dmem. See RiscvProcessor for clock details.
    input wire clock_mem,

    // must always be aligned to a 4B boundary
    input wire [`REG_SIZE] pc_to_imem,

    // the value at memory location pc_to_imem
    output logic [`INSN_SIZE] insn_from_imem,

    // must always be aligned to a 4B boundary
    input wire [`REG_SIZE] addr_to_dmem,

    // the value at memory location addr_to_dmem
    output logic [`REG_SIZE] load_data_from_dmem,

    // the value to be written to addr_to_dmem, controlled by store_we_to_dmem
    input wire [`REG_SIZE] store_data_to_dmem,

    // Each bit determines whether to write the corresponding byte of store_data_to_dmem to memory location addr_to_dmem.
    // E.g., 4'b1111 will write 4 bytes. 4'b0001 will write only the least-significant byte.
    input wire [3:0] store_we_to_dmem
);

  // memory is arranged as an array of 4B words
  logic [`REG_SIZE] mem_array[NUM_WORDS];

`ifdef SYNTHESIS
  initial begin
    $readmemh("mem_initial_contents.hex", mem_array);
  end
`endif

  always_comb begin
    // memory addresses should always be 4B-aligned
    assert (pc_to_imem[1:0] == 2'b00);
    assert (addr_to_dmem[1:0] == 2'b00);
  end

  localparam int AddrMsb = $clog2(NUM_WORDS) + 1;
  localparam int AddrLsb = 2;

  always @(posedge clock_mem) begin
    if (rst) begin
    end else begin
      insn_from_imem <= mem_array[{pc_to_imem[AddrMsb:AddrLsb]}];
    end
  end

  always @(negedge clock_mem) begin
    if (rst) begin
    end else begin
      if (store_we_to_dmem[0]) begin
        mem_array[addr_to_dmem[AddrMsb:AddrLsb]][7:0] <= store_data_to_dmem[7:0];
      end
      if (store_we_to_dmem[1]) begin
        mem_array[addr_to_dmem[AddrMsb:AddrLsb]][15:8] <= store_data_to_dmem[15:8];
      end
      if (store_we_to_dmem[2]) begin
        mem_array[addr_to_dmem[AddrMsb:AddrLsb]][23:16] <= store_data_to_dmem[23:16];
      end
      if (store_we_to_dmem[3]) begin
        mem_array[addr_to_dmem[AddrMsb:AddrLsb]][31:24] <= store_data_to_dmem[31:24];
      end
      // dmem is "read-first": read returns value before the write
      load_data_from_dmem <= mem_array[{addr_to_dmem[AddrMsb:AddrLsb]}];
    end
  end
endmodule

/*
This shows the relationship between clock_proc and clock_mem. The clock_mem is
phase-shifted 90° from clock_proc. You could think of one proc cycle being
broken down into 3 parts. During part 1 (which starts @posedge clock_proc)
the current PC is sent to the imem. In part 2 (starting @posedge clock_mem) we
read from imem. In part 3 (starting @negedge clock_mem) we read/write memory and
prepare register/PC updates, which occur at @posedge clock_proc.

        ____
 proc: |    |______
           ____
 mem:  ___|    |___
*/
module Processor (
    input wire               clock_proc,
    input wire               clock_mem,
    input wire               rst,
    output wire [`REG_SIZE]  trace_completed_pc,
    output wire [`INSN_SIZE] trace_completed_insn,
    output cycle_status_e    trace_completed_cycle_status, 
    output logic             halt
);

  wire [`REG_SIZE] pc_to_imem, mem_data_addr, mem_data_loaded_value, mem_data_to_write;
  wire [`INSN_SIZE] insn_from_imem;
  wire [3:0] mem_data_we;

  // This wire is set by cocotb to the name of the currently-running test, to make it easier
  // to see what is going on in the waveforms.
  wire [(8*32)-1:0] test_case;

  MemorySingleCycle #(
      .NUM_WORDS(8192)
  ) memory (
      .rst      (rst),
      .clock_mem (clock_mem),
      // imem is read-only
      .pc_to_imem(pc_to_imem),
      .insn_from_imem(insn_from_imem),
      // dmem is read-write
      .addr_to_dmem(mem_data_addr),
      .load_data_from_dmem(mem_data_loaded_value),
      .store_data_to_dmem (mem_data_to_write),
      .store_we_to_dmem  (mem_data_we)
  );

  DatapathSingleCycle datapath (
      .clk(clock_proc),
      .rst(rst),
      .pc_to_imem(pc_to_imem),
      .insn_from_imem(insn_from_imem),
      .addr_to_dmem(mem_data_addr),
      .store_data_to_dmem(mem_data_to_write),
      .store_we_to_dmem(mem_data_we),
      .load_data_from_dmem(mem_data_loaded_value),
      .trace_completed_pc(trace_completed_pc),
      .trace_completed_insn(trace_completed_insn),
      .trace_completed_cycle_status(trace_completed_cycle_status),
      .halt(halt)
  );

endmodule
